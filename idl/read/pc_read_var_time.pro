; +
; NAME:
;       PC_READ_VAR_TIME
;
; PURPOSE:
;       Read time of a given var.dat, or other VAR file.
;
;       Returns the time from a snapshot (var) file generated by Pencil Code.
;
; CATEGORY:
;       Pencil Code, File I/O
;
; CALLING SEQUENCE:
;       pc_read_var_time, time=time, varfile=varfile, datadir=datadir, /quiet
; KEYWORD PARAMETERS:
;    datadir: Specifies the root data directory. Default: './data'.  [string]
;    varfile: Name of the var file. Default: 'var.dat'.              [string]
;   allprocs: Load data from the allprocs directory.                 [integer]
;   /reduced: Load reduced collective varfiles.
;
;       time: Variable in which to return the loaded time.           [real]
;exit_status: Suppress fatal errors in favour of reporting the
;             error through exit_status/=0.
;
;     /quiet: Suppress any information messages and summary statistics.
;      /help: Display this usage information, and exit.
;
; EXAMPLES:
;       pc_read_var, time=t              ;; read time into variable t
;
; MODIFICATION HISTORY:
;       $Id$
;       Written by: Antony J Mee (A.J.Mee@ncl.ac.uk), 27th November 2002
;
;-
pro pc_read_var_time,                                                              $
    time=time, varfile=varfile_, allprocs=allprocs, datadir=datadir, param=param,  $
    procdim=dim, ivar=ivar, swap_endian=swap_endian, f77=f77, reduced=reduced,     $
    exit_status=exit_status, quiet=quiet

COMPILE_OPT IDL2,HIDDEN
;
; Use common block belonging to derivative routines etc. so we can
; set them up properly.
;
  common pc_precision, zero, one
  common cdat_coords,coord_system
;
; Default settings.
;
  default, reduced, 0
  default, quiet, 0

  if (keyword_set(reduced)) then allprocs=1

  if not is_defined(allprocs) then begin
;
; derive allprocs from the setting in Makefile.local
;
    spawn, "grep '^ *[^\#].*io_collect' src/Makefile.local", grepres, greperr

    if strpos(grepres,'xy') ge 0 then $
      allprocs=2 $
    else if grepres ne '' then $
      allprocs=1 $
    else $
      allprocs=0
  endif

  if (arg_present(exit_status)) then exit_status=0
;
; Set f77 keyword according to allprocs.
;
  if (keyword_set (allprocs)) then $
    if (allprocs eq 1) then default, f77, 0
  default, f77, 1
;
; Default data directory.
;
  if (not keyword_set(datadir)) then datadir=pc_get_datadir()
;
; Name and path of varfile to read.
;
  if (n_elements(ivar) eq 1) then begin
    default, varfile_, 'VAR'
    varfile=varfile_+strcompress(string(ivar),/remove_all)
  endif else begin
    default, varfile_, 'var.dat'
    varfile=varfile_
  endelse
;
; Get necessary dimensions quietly.
;
  if (n_elements(dim) eq 0) then begin
    if (allprocs eq 1) then begin
      pc_read_dim, object=dim, datadir=datadir, reduced=reduced, /quiet
    endif else if (allprocs eq 2) then begin
      pc_read_dim, object=dim, datadir=datadir, proc=0, /quiet
      dim.nx = dim.nxgrid
      dim.ny = dim.nygrid
      dim.mx = dim.mxgrid
      dim.my = dim.mygrid
      dim.mw = dim.mx * dim.my * dim.mz
    endif else begin
      pc_read_dim, object=dim, datadir=datadir, proc=0, /quiet
    endelse
  endif
  if (n_elements(param) eq 0) then $
      pc_read_param, object=param, dim=dim, datadir=datadir, /quiet
;
; ... and check pc_precision is set for all Pencil Code tools.
;
  pc_set_precision, dim=dim, quiet=quiet
;
; Local shorthand for some parameters.
;
  mvar = dim.mvar
  mvar_io = mvar
  if (param.lwrite_aux) then mvar_io += dim.maux
  precision = dim.precision
  if (precision eq 'D') then bytes = 8 else bytes = 4
;
; Initialize / set default returns for ALL variables.
;
  t=zero
  x=fltarr(dim.mx)*one
  y=fltarr(dim.my)*one
  z=fltarr(dim.mz)*one
  dx=zero
  dy=zero
  dz=zero
  deltay=zero
;
; Get a free unit number.
;
  get_lun, file
;
; Build the full path and filename.
;
  if (allprocs eq 1) then begin
    if (keyword_set (reduced)) then dirname='/reduced/' else dirname='/allprocs/'
  end else begin
    dirname='/proc0/'
  end
  filename=datadir+dirname+varfile
;
; Check for existence and read the data.
;
  if (not file_test(filename)) then begin
    if (arg_present(exit_status)) then begin
      exit_status=1
      print, 'ERROR: cannot find file '+ filename
      close, /all
      return
    endif else begin
      message, 'ERROR: cannot find file '+ filename
    endelse
  endif
;
; Open a varfile and read some data!
;
  openr, file, filename, /f77, swap_endian=swap_endian
  if (precision eq 'D') then bytes=8 else bytes=4
  if (f77 eq 0) then markers=0 else markers=2
  point_lun, file, long64(dim.mx)*long64(dim.my)*long64(dim.mz)*long64(mvar_io*bytes)+long64(markers*4)
  if (allprocs eq 1) then begin
    ; collectively written files
    readu, file, t, x, y, z, dx, dy, dz
  endif else if (allprocs eq 2) then begin
    ; xy-collectively written files for each ipz-layer
    readu, file, t
  endif else begin
    ; distributed files
    if (param.lshear) then begin
      readu, file, t, x, y, z, dx, dy, dz, deltay
    endif else begin
      readu, file, t, x, y, z, dx, dy, dz
    endelse
  endelse
;
  close,file
  free_lun,file
;
; If requested print a summary (actually the default - unless being quiet).
;
  if (not quiet) then print, ' t = ', t
  time = t
;
end
